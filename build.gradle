/*
 * Copyright (c) 2022 Vikash Madhow
 */

/**
 * Encodes ESQL Result into different forms suited for transmission.
 *
 * @author Vikash Madhow (vikash.madhow@gmail.com)
 */

plugins {
  id "java-library"
  id "maven-publish"
  id "synapticloop.projectFilestatistics" version "1.1.0"
}

group = 'ma.vi'
version "0.3.5"
sourceCompatibility = JavaVersion.VERSION_17

project.ext {
  moduleName = project.group + '.' + project.name
  isReleaseVersion = !version.endsWith("SNAPSHOT")
}

projectFilestatistics {
  // the files to include in the generation of the statistics
  includes = [
      "src/**/*.*",
  ]

  // the files to exclude from the generation
  excludes = [
      "**/node_modules/*",
      "**/*.woff*",
      "**/*.babelrc",
      "**/*.editorconfig",
      "**/*.eot",
      "**/*.eslintignore",
      "**/*.gif",
      "**/*.ico",
      "**/*.interp",
      "**/*.jpg",
      "**/*.json",
      "**/*.keep",
      "**/*.map",
      "**/*.orig",
      "**/*.otf",
      "**/*.png",
      "**/*.svg",
      "**/*.tokens",
      "**/*.ttf",
      "**/*.xls*",
      "**/*.pfx",
      "**/dist/*"
  ]

  // the output directory for the generated statistics
  outputDirectory = "$rootProject.buildDir/docs"

  // if you require to over-ride how single and multi-line comments work, or
  // need to add in some extensions which aren't built in...
  // propertyFile = 'over-ride.properties'
}

jar {
  inputs.property("moduleName", project.moduleName)
  manifest {
    attributes('Automatic-Module-Name': project.moduleName)
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation("ma.vi:esql:latest.release")
  implementation("commons-io:commons-io:latest.release")
  implementation("org.apache.commons:commons-lang3:latest.release")

  testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
  useJUnitPlatform()
}